#!/usr/bin/perl
#
# $Id: onyphe,v d629ccff5f0c 2018/12/03 12:59:47 gomor $
#
use strict;
use warnings;

use Getopt::Long;
use Metabrik::Client::Onyphe;

# Default values
my %lopts = (
   verbose => 0,
   onyphe_rc => $ENV{HOME}.'/.onyphe_rc',
   output => 'json',
   page => '1',
   autoscroll => '0',
   apiurl => 'https://www.onyphe.io/api/v2',
   wait => 2,
);

# Option parsing
GetOptions(
   "user" => \$lopts{user},
   "simple=s" => \$lopts{simple},
   "summary=s" => \$lopts{summary},
   "value=s" => \$lopts{value},
   "search=s" => \$lopts{search},
   "export=s" => \$lopts{export},
   "verbose=i" => \$lopts{verbose},
   "apikey=s" => \$lopts{apikey},
   "output=s" => \$lopts{output},
   "fields=s" => \$lopts{fields},
   "encode=s" => \$lopts{encode},
   "page=i" => \$lopts{page},
   "maxpage=i" => \$lopts{maxpage},
   "autoscroll=i" => \$lopts{autoscroll},
   "apiurl=s" => \$lopts{apiurl},
   "wait=i" => \$lopts{wait},
   "master" => \$lopts{master},
) or usage();

# Load API key from onyphe rc file
load_onyphe_rc(%lopts);

# Sanity checks
sanity_checks(%lopts);

# Ready to rock
my $cli = Metabrik::Client::Onyphe->new_brik_init;
$cli->log->level($lopts{verbose});
$cli->apikey($lopts{apikey});
$cli->apiurl($lopts{apiurl});
$cli->autoscroll($lopts{autoscroll} ? 1 : 0);
$cli->wait($lopts{wait});
$cli->master($lopts{master} ? 1 : 0);

# Launch query
my $r;
if ($lopts{search}) {   # Search API query mode with pipeline support
   $r = $cli->search_pipeline($lopts{search}, $lopts{page}, $lopts{maxpage})
      or $cli->log->fatal("search_pipeline: aborted");
}
elsif ($lopts{export}) {   # Export API query mode
   # Export does not return results as it streams them, so there is no
   # output mode to use at the end.
   $cli->export_pipeline($lopts{export})
      or $cli->log->fatal("export_pipeline: aborted");
}
elsif ($lopts{summary}) {
   my $summary = $lopts{summary};
   my $value = $lopts{value};
   $r = $cli->summary($summary, $value)
      or $cli->log->fatal("summary: aborted");
}
elsif ($lopts{user}) {
   $r = $cli->user() or $cli->log->fatal("user: aborted");
}
else {  # Simple API query mode
   my $simple = $lopts{simple};
   my $value = $lopts{value};
   $r = $cli->simple($simple, $value, $lopts{page}, $lopts{maxpage})
      or $cli->log->fatal("simple: aborted");
}

# Output results
if (defined($r)) {
   my $output_mode = 'output_'.$lopts{output};
   my $output = $cli->$output_mode($r, $lopts{fields}, $lopts{encode});
   if (! defined($output)) {
      $cli->log->fatal("$output_mode: aborted");
   }
   print $output;
}

exit(0);

#
# Local subroutines
#

sub usage {
   print<<EOF

Usage: onyphe [options] -search 'category:CATEGORY SEARCH'
       onyphe [options] -user
       onyphe [options] -simple API -value VALUE
       onyphe [options] -summary API -value VALUE

   -user                                   get information on your license
   -apikey <APIKEY>                        your personal ONYPHE API key
   -search <SEARCH>                        use Search API for query
   -summary <API>                          use Summary API for query (need -value)
   -simple <API>                           use Simple API for query (need -value)
   -value <VALUE>                          value to use when using Simple or Summary API
   -output <MODE>                          output mode (default: json)
                                           Modes: dump, csv, psv, json
   -page <PAGE>                            fetch results for page (default: 1)
   -autoscroll <0|1>                       use autoscroll feature (default: 0)
                                           WARNING: may consume a lot of credit
   -maxpage <MAXPAGE>                      max page to fetch in autoscroll mode
   -fields <-|+><FIELD1,FIELD2,...FIELDn>  comma separated list of fields to display (+)
                                           or remove (-) from output
   -encode <FIELD1,FIELD2,...FIELDn>       comma separated list of fields to Base64 encode
   -verbose <0|1|2|3>                      verbosity level (default: 0)
   -wait <SECONDS>                         wait time in regards to rate limit (default: 2)

EOF
;

   exit(0);
}

sub load_onyphe_rc {
   my ($lopts) = @_;

   # If already given from command line, do not read rc file for apikey
   if (length($lopts{apikey})) {
      return 1;
   }

   if (-f $lopts{onyphe_rc}) {
      open(my $in, '<', $lopts{onyphe_rc})
         or die("ERROR: open: ".$lopts{onyphe_rc}.": failed: $!");
      chomp(my $apikey = <$in>);
      if (length($apikey) && $apikey =~ m{^[a-z0-9]{40}$}) {
         $lopts{apikey} = $apikey;
      }
      else {
         die("ERROR: apikey [$apikey] invalid format found in ".$lopts{onyphe_rc}.
            " file");
      }
      close($in);
   }

   return 1;
}

sub sanity_checks {
   my ($lopts) = @_;

   if (! defined($lopts{search}) && ! defined($lopts{simple})
   &&  ! defined($lopts{summary}) && ! defined($lopts{user})
   &&  ! defined($lopts{export})) {
      print "ERROR: need -search, -simple, -summary, -export or -user argument\n";
      usage();
   }

   if (! defined($lopts{apikey})) {
      print "ERROR: -apikey argument missing\n";
      usage();
   }

   if (($lopts{simple} || $lopts{summary}) && ! defined($lopts{value})) {
      print "ERROR: -simple or -summary argument used but -value argument missing\n";
      usage();
   }

   if (! ($lopts{simple} || $lopts{summary}) && defined($lopts{value})) {
      print "ERROR: -simple argument missing but -value argument used\n";
      usage();
   }

   if ($lopts{output} !~ m{^(?:csv|psv|dump|json)$}) {
      print "ERROR: invalid output chosen: ".$lopts{output}."\n";
      usage();
   }

   if ($lopts{page} !~ m{^\d+$}) {
      print "ERROR: invalid page chosen: ".$lopts{page}."\n";
      usage();
   }

   if (defined($lopts{maxpage}) && $lopts{maxpage} !~ m{^\d+$}) {
      print "ERROR: invalid maxpage chosen: ".$lopts{maxpage}."\n";
      usage();
   }

   if ($lopts{verbose} !~ m{^\d+$}) {
      print "ERROR: invalid verbose mode chosen: ".$lopts{verbose}."\n";
      usage();
   }

   return 1;
}

1;

__END__

=head1 COPYRIGHT AND LICENSE

Copyright (c) 2018-2020, ONYPHE

You may distribute this module under the terms of The BSD 3-Clause License.
See LICENSE file in the source distribution archive.

=head1 AUTHOR

ONYPHE

=cut
